// Similarities like Pearson, Cosine, and Adjusted Cosine Similarity between items.
1.0 : rated(U,I1) & rated(U,I2) & rating(U,I1) & sim_cosine_items(I1,I2) >> rating(U,I2)^2

// Similarities like Pearson and Cosine Similarity between users.
1.0 : rated(U1,I) & rated(U2,I) & rating(U1,I) & sim_cosine_users(U1,U2) >> rating(U2,I)^2

// Predictions by different other methods like SGD, Item based Pearson methods, and BPMF methods.

// Average prior of user rating and item ratings.
1.0 : user(U) & item(I) & rated(U,I) & avg_user_rating(U) >> rating(U,I)^2
1.0 : user(U) & item(I) & rated(U,I) & rating(U,I) >> avg_user_rating(U)^2
1.0 : user(U) & item(I) & rated(U,I) & avg_item_rating(I) >> rating(U,I)^2
1.0 : user(U) & item(I) & rated(U,I) & rating(U,I) >> avg_item_rating(I)^2

// Content rule by Jaccard similarity.
1.0 : rated(U,I1) & rated(U,I2) & rating(U,I1) & sim_content_items_jaccard(I1,I2) >> rating(U,I2)^2

// Preference Rules
item_preference(U1, I1, I2) + item_preference(U1, I2, I1) = 1.
1.0 : item_preference(U1, I1, I2) & sim_cosine_users(U1, U2) & rated(U1,I1) & rated(U1,I2) & rated(U2,I1) & rated(U2,I2) -> item_preference(U2, I1, I2) ^2
1.0 : item_preference(U, I1, I2) & sim_cosine_items(I2, I3) & rated(U,I1) & rated(U,I2) & rated(U,I3) -> item_preference(U, I1, I3) ^2
1.0 : item_preference(U, I1, I2) & item_preference(U, I2, I3) & rated(U,I1) & rated(U,I2) & rated(U,I3) -> item_preference(U, I1, I3) ^2

// Point to Pairwise relation rules.  only one direction so pref can be soft. this direction so rating at least reflects preference
1.0: 0.5 * rating(U1, I1) - 0.5 * rating(U1, I2) + 0.5 <= item_preference(U1, I1, I2) ^2